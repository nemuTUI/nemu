# Maintainer: MaksVal <valmaxster@gmail.com> -> https://github.com/MaksVal
# Contributor: Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Sergey Malkin  <adresatt@gmail.com>

pkgname=nemu
pkgver=2.6.0
pkgrel=1
pkgdesc="ncurses interface for QEMU"
arch=(i686 x86_64)
url="https://github.com/nemuTUI/nemu"
license=(BSD)
depends=(qemu ncurses sqlite udev libusb)
makedepends=(cmake)
source=("$pkgname-$pkgver.tar.gz::https://github.com/nemuTUI/nemu/archive/refs/tags/v${pkgver}.tar.gz"
		"https://raw.githubusercontent.com/nemuTUI/nemu/master/LICENSE")
sha256sums=('dd0467cb82aae9945416bbadd8279d0aadf1d56ce5c6e7ccfc3116fa91b674e7'
            '0fdc74f6ec229b373f65c5b4621d914151562cd674780d1606f04d24854b2f8c')

prepare() {
  cd "${pkgname}-${pkgver}"
  install -d build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DNM_WITH_NETWORK_MAP=ON -DNM_WITH_DBUS=ON
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir/" install
  install -D -m644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
